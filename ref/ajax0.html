Ajax is surprisingly easy to use with the Arc language.  This article shows how to implement Ajax-based autocompletion and dynamic page updates using 
the <a href="http://wiki.script.aculo.us/scriptaculous/">script.aculo.us</a> and <a href="http://prototypejs.org/">Prototype</a> JavaScript frameworks.  These frameworks allow Ajax applications to be implemented with just a few lines of additional code.
The hard work is done by the script.aculo.us and Prototype libraries.  All the Arc code needs to do is provide the list of autocompletion candidates, and the dynamic page content.

<h3>The example</h3>
This example implements a simple Ajax input autocompleter and dynamic page update.  For the autocompleter, as soon as you enter a few letters into the input, it provides a list of matching terms. As soon as you select a term, the page is dynamically updated with associated data, as in the following screenshot:
<br>
<img style="border:1px solid #000000;" src="/ajax/ajax.gif" alt="screenshot">
<br>
For the purposes of the demonstration, the content is information on the
countries of the world, obtained from the <a
href="https://www.cia.gov/library/publications/the-world-factbook/">CIA World
Factbook</a>.
<p>
To summarize how it all works:
When you enter characters in the input field, the script.aculo.us
autocompleter JavaScript sends the charaters to the Arc server, which returns the
autocomplete suggestions.  The autocompleter JavaScript displays the
suggestions on the page.
When you select a country, the updater JavaScript does three separate requests
to the Arc server, to request the population, area, and capital.  When the
responses arrive, they are inserted into the page.
<p>
In more detail, the autocompletion is performed by <a href="http://wiki.script.aculo.us/scriptaculous/show/Ajax.Autocompleter">Ajax.Autocompleter</a>, and the dynamic updating is performed by <a href="http://www.prototypejs.org/api/ajax/updater">Ajax.Updater</a>.  The control flow is:
<ul>
<li>
The <code>Ajax.Autocompleter</code> associated with the <code>autocomplete</code> input and <code>autocomplete_choices</code> div sends inputs to <code>/auto_complete?prefix=<i>chars</i></code>.
<li>The Arc handler sends back a formatted list of autocomplete candidates, e.g.
<code>
&lt;ul&gt;&lt;li&gt;Iran&lt;/li&gt;&lt;li&gt;Iraq&lt;/li&gt;&lt;li&gt;Ireland&lt;/li&gt;&lt;/ul&gt;</code>.
<li>The <code>Ajax.Autocompleter</code> displays the candidates in the <code>autocomplete</code> div.
<li>When an autocompletion is selected, the <code>update</code> JavaScript function starts three <code>Ajax.Updater</code> instances, which send an Ajax request to URL <code>/getcontents?field=population&amp;name=<i>value</i>, and similarly for area and capital in parallel.
<li>The Arc handler for <code>getcontents</code> returns the desired contents.
<li><code>Ajax.Updater</code> puts the contents into the <code>population</code>, <code>area</code>, and <code>capital</code> divs.
</ul>
<h3>Running the example</h3>
First set up the necessary files.
<ul>
<li>Download <a href="/ajax/ajax.zip">ajax.zip</a> and uncompress it into the
same directory that Arc runs in.  This zip file provides
<code>ajax.html</code>, <code>ajax.arc</code>, <code>data.arc</code>, and <code>autocomplete.css</code>.
<li> Download the script.aculo.us library files from <a href="http://script.aculo.us/downloads">script.aculo.us</a>.  Copy the <code>.js</code> files from <code>lib</code> and <code>src</code> into the same directory that Arc runs in.
</ul>
Next, start up the Arc interpreter,
load the <code>ajax.arc</code> file, and start the web server.
<pre class="arc">
arc> (load "ajax.arc")
arc> (thread (serve 8080))
arc> ready to serve port 8080
</pre>
Then go to <a href="http://localhost:8080/ajax.html">http://localhost:8080/ajax.html</a>.  (Unfortunately I don't have a live demo on a public machine.  If anyone sets one up, let me know and I'll add a link here.)
<p>
Start typing in the box, and you should see a dropdown with autocompleted choices.  Click one, and the code will be displayed below asynchronously.

<h3>The Arc code</h3>
The Arc server code is in <a href="/ajax/ajax.arc"><code>ajax.arc</code></a>. Two Arc handlers are implemented to provide the client-side Ajax support.  The first, <code>auto_complete</code> receives the current input contents and returns a list of up to 10 autocompletion candidates formatted in HTML.
The second handler, <code>getcontents</code> returns the dynamic content for the page, from the <code>database</code> table.  Note that the handlers do nothing particularly special to make them "Ajax"; they are standard Arc web handlers based on <code>defop</code> and can be accessed directly through the browser.  See <a href="http://arcfn.com/doc/srv.html">Arc Web Server</a> for more information on web serving with Arc.

<pre class="code">
(defop auto_complete req
  (let prefix (downcase (arg req "prefix"))
    (prn (to-html-list (cut (startselts prefix keylist) 0 10)))))

(defop getcontents req 
  (with (field (arg req "field")  name (arg req "name"))
    (if (is field "population") (prn ((database name) 1))
        (is field "area") (prn ((database name) 2))
        (is field "capital") (prn ((database name) 3)))))
</pre>
The handlers use a couple helpers; <code>startselts</code> returns the elements that match the autocomplete prefix and <code>to-html-list</code> wraps the elements in HTML list tags.
<pre class="code">
; Returns a list of elements that start with prefix
(def startselts (prefix seq) (rem [no (begins (downcase _) prefix)] seq))

; Wraps elts in HTML list tags
(def to-html-list (elts) (tostring
  (prall elts "&lt;ul&gt;&lt;li&gt;" "&lt;/li&gt;&lt;li&gt;")
  (pr "&lt;/li&gt;&lt;/ul&gt;")))
</pre>
The actual content is obtained from <a href="/ajax/data.arc"><code>data.arc</code></a>, which contains the information on each country as a list of lists.
<pre class="code">
("United States" "301,139,947" "9,826,630" "Washington, DC")
</pre>
Some simple code converts <code>data.arc</code> into a table called <code>database</code> indexed by country for easy lookup, and generates a sorted list of countries for use in autocompletion:
<pre class="code">
(= database (table))
(w/infile inf "data.arc"
  (let datalist (sread inf nil)
    (each elt datalist
      (= (database (elt 0)) elt))))
(= keylist (mergesort &lt; (keys database)))
</pre>
<p>
For some reason, the default Arc web server <code>srv.arc</code> doesn't support <code>.js</code> files.  The <code>ajax.arc</code> file modifies the server slightly to support these files:
<pre class="code">
(= (srv-header* 'text/javascript) 
"HTTP/1.0 200 OK
Content-Type: text/javascript; charset=UTF-8
Connection: close")

(def static-filetype (sym)
  (let fname (string sym)
    (and (~find #\/ fname)
         (case (last (check (tokens fname #\.) ~single))
           "gif"  'gif
           "jpg"  'jpg
           "css"  'text/css
           "txt"  'text/html
           "html" 'text/html
           "js"   'text/javascript
           ))))
</pre>
<h3>Debugging</h3>
Several things can go wrong when trying to run the example.  If the initial page doesn't load at all, something is probably wrong with the Arc server.  If no autocompletion happens, the JavaScript may not be loading, or the Arc server may have problems.  If the autocompletion shows up as a bulleted list, the CSS file is probably not loading.
<p>
The Arc code can be debugged in several ways.  The first is to access the handlers directly.  The autocomplete handler <a href="http://localhost:8080/auto_complete?prefix=a">http://localhost:8080/auto_complete?prefix=a</a> should return a bullet list of autocomplete suggestions.  The contents handler: <a href="http://localhost:8080/getcontents?field=area&name=Algeria">http://localhost:8080/getcontents?field=area&name=Algeria</a> should return the dynamic contents value.
<p>
The Arc code can also be debugged by strategically inserting print statements such as:
<pre class="code">
(write req (stderr))
</pre>
This will display the request on the Arc console.
<p>
To debug Ajax, you can use Firefox plugins
<a href="http://livehttpheaders.mozdev.org/">Live HTTP Headers</a> and
<a href="http://www.getfirebug.com/">Firebug</a>.  Live HTTP Headers lets you see the headers between the browser and the server, and is very helpful to determine if something is failing.  Firebug allows much more in-depth JavaScript debugging.
 The browser's JavaScript console is also useful to see JavaScript errors.
<p>
This article has just scratched the surface of script.aculo.us and Ajax.  More information is available at the <a href="http://script.aculo.us/">script.aculo.us</a> website or in <a href="http://www.amazon.com/exec/obidos/ASIN/1934356018/rightocom">books</a>.
